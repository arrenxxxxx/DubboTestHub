plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
}

allprojects {
    group = 'io.arrenxxxxx'
    version = '0.0.2-RELEASE'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    
    sourceCompatibility = '17'
    targetCompatibility = '17'
    
    compileJava {
        options.compilerArgs << '-parameters'
    }
    
    tasks.withType(JavaExec) {
        jvmArgs = [
            '--add-opens=java.base/java.math=ALL-UNNAMED',
            '--add-opens=java.base/java.lang=ALL-UNNAMED',
            '--add-opens=java.base/java.util=ALL-UNNAMED',
            '--add-opens=java.base/java.util.concurrent=ALL-UNNAMED',
            '--add-opens=java.base/java.net=ALL-UNNAMED'
        ]
    }
    
    repositories {
        mavenCentral()
    }
    
    ext {
        springBootVersion = '3.2.0'
        dubboVersion = '3.2.10'
        nacosVersion = '2.2.4'
        zookeeperVersion = '3.8.3'
        curatorVersion = '5.6.0'
        lombokVersion = '1.18.30'
    }
    
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
        dependencies {
            // Dubbo
            dependency "org.apache.dubbo:dubbo:${dubboVersion}"
            dependency "org.apache.dubbo:dubbo-spring-boot-starter:${dubboVersion}"
            // Nacos
            dependency "com.alibaba.nacos:nacos-client:${nacosVersion}"
            dependency "org.apache.dubbo:dubbo-registry-nacos:${dubboVersion}"
            // Zookeeper
            dependency "org.apache.zookeeper:zookeeper:${zookeeperVersion}"
            dependency "org.apache.dubbo:dubbo-registry-zookeeper:${dubboVersion}"
            dependency "org.apache.curator:curator-framework:${curatorVersion}"
            dependency "org.apache.curator:curator-recipes:${curatorVersion}"
            // Lombok
            dependency "org.projectlombok:lombok:${lombokVersion}"
        }
    }
    
    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
        testCompileOnly "org.projectlombok:lombok"
        testAnnotationProcessor "org.projectlombok:lombok"
    }
} 